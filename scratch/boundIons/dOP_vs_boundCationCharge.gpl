set term pdfcairo enhanced rounded color dashed dashlength 1 size 16.2cm, 20cm #10cm #21cm
set out "dOP_vs_boundCationCharge_g3.pdf"

set key invert

w=2 # linewidth simulation
r=0.5  # point radius simulation

# Line- and point types for different force fields:
set style line 15 lt 1 pt 11 ps r lw w lc rgb "#D70039" # for Berger DLPC
set style line 11 lt 2 pt 7 dt 3 ps r lw w lc rgb "#D70039" # for Berger POPC
set style line 12 lt 1 pt 7 ps r lw w lc rgb "#0088BF" # for CHARMM36
set style line 13 lt 1 pt 7 ps r lw w lc rgb "#FFD300" # for Lipid14
set style line 14 lt 1 pt 7 ps r lw w lc rgb "#00A368" # for MacRog

set style fill transparent solid 0.25 noborder

set multiplot layout 2, 1 #tit " "
set xlabel "bound cation charge (q/nm^{2}, integrated until C_{g3})"
set ylabel "change of C_{beta} order parameter from pure water case"
plot \
  "boundDensity_vs_dOP.macrog" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls 14 t "", \
  "boundDensity_vs_dOP.c36" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls 12 t "", \
  "boundDensity_vs_dOP.lipid14" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls 13 t "", \
  "boundDensity_vs_dOP.macrog" u ($1):($4-$2) ev :::::1 w lp ls 14 t "MacRog", \
  "boundDensity_vs_dOP.lipid14" u ($1):($4-$2) ev :::::1 w lp ls 13 t "Lipid14", \
  "boundDensity_vs_dOP.c36" u ($1):($4-$2) ev :::::1 w lp ls 12 t "CHARMM36"

set ylabel "change of C_{alpha} order parameter from pure water case"
plot \
  "boundDensity_vs_dOP.macrog" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls 14 t "", \
  "boundDensity_vs_dOP.c36" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls 12 t "", \
  "boundDensity_vs_dOP.lipid14" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls 13 t "", \
  "boundDensity_vs_dOP.macrog" u ($1):($4-$2) ev :::2 w lp ls 14 t "MacRog", \
  "boundDensity_vs_dOP.lipid14" u ($1):($4-$2) ev :::2 w lp ls 13 t "Lipid14", \
  "boundDensity_vs_dOP.c36" u ($1):($4-$2) ev :::2 w lp ls 12 t "CHARMM36"

unset multiplot