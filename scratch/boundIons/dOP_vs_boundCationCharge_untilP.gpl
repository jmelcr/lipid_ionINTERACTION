set term pdfcairo enhanced rounded color dashed dashlength 1 size 10cm, 12.8cm
top = 0.99
heightY = 0.46
right = 0.975
left = 0.14
set out "dOP_vs_boundCationCharge_P.pdf"

set key at graph 0.25,0.09 reverse Left bottom left maxcolumn 2 maxrows 5 samplen 1 #opaque
set border back # draws border lines behind everything, so key will be on top

w=2 # line width
pw=1 # line width in points
ls_forKey = 10
ls_berger = 11
ls_beOPLS = 111
ls_chrm36 = 12
ls_lipd14 = 13
ls_macrog = 14
ls_orange = 15
ls_ulmsch = 16
ls_slipid = 17

r=0.65  # point radius
set pointsize r
pt_DPPC_Na = 4
pt_POPC_Na = 6
pt_POPC_Ca = 7

set style fill transparent solid 0.3 noborder

# Line- and point types for different force fields:
set style line ls_forKey lt 1 ps 1.5*r lc rgb "black"
set style line ls_berger lt 1 dt 1 lw w lc rgb "#D70039"
set style line ls_beOPLS lt 1 dt 3 lw w lc rgb "#D70039"
set style line ls_chrm36 lt 1 dt 1 lw w lc rgb "#0088BF"
set style line ls_lipd14 lt 1 dt 1 lw w lc rgb "#FFD300"
set style line ls_macrog lt 1 dt 1 lw w lc rgb "#00A368"
set style line ls_orange lt 1 dt 1 lw w lc rgb "#FF6600"
set style line ls_ulmsch lt 1 dt 1 lw w lc rgb "#FFA0A3"
set style line ls_slipid lt 1 dt 1 lw w lc rgb "#7AEE9E"
set multiplot layout 2, 1 #tit " "

set rmargin at screen right
set lmargin at screen left
set tmargin at screen top
set bmargin at screen top-heightY
unset xlabel
set xtics format '' 0.1
set mxtics 2
set ytics format "%3.2f" 0.02
set mytics 2
set ylabel "change of C-H order parameter from salt-free solution ({/Symbol D}S_{CH}^{/Symbol i})" offset 0.8,-9
set label 100 "{/Symbol b}" at graph 0.06, 0.11 font ",25"
plot [:][-0.17:0.03] \
  "boundDensity_vs_dOP_untilP.orange"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_orange t "", \
  "boundDensity_vs_dOP_untilP.berger"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_berger t "", \
  "boundDensity_vs_dOP_untilP.berger-OPLS"   u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_beOPLS t "", \
  "boundDensity_vs_dOP_untilP.macrog"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_macrog t "", \
  "boundDensity_vs_dOP_untilP.c36"           u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_chrm36 t "", \
  "boundDensity_vs_dOP_untilP.lipid14"       u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_lipd14 t "", \
  "boundDensity_vs_dOP_untilP.ulmschneiders" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_ulmsch t "", \
  "boundDensity_vs_dOP_untilP.slipid"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::::1 w filledcurves ls ls_slipid t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.orange" ev :::0::0 u ($1):($4-$2) w p ls ls_orange lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.orange" ev :::1::1 u ($1):($4-$2) w p ls ls_orange lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::0::0 u ($1):($4-$2) w p ls ls_berger lw pw pt pt_DPPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::1::1 u ($1):($4-$2) w p ls ls_berger lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger" ev :::2::2 u ($1):($4-$2) w p ls ls_berger lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP.berger-OPLS" ev ::1:::1 u ($1):($4-$2) w p ls ls_beOPLS lw pw pt pt_DPPC_Na t "", \
   "boundDensity_vs_dOP_untilP.macrog" ev ::1:::1 u ($1):($4-$2) w p ls ls_macrog lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.c36" ev :::0::0 u ($1):($4-$2) w p ls ls_chrm36 lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.c36" ev :::1::1 u ($1):($4-$2) w p ls ls_chrm36 lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::0::0 u ($1):($4-$2) w p ls ls_lipd14 lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::1::1 u ($1):($4-$2) w p ls ls_lipd14 lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP.ulmschneiders"          ev ::1:::1 u ($1):($4-$2) w p ls ls_ulmsch lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP.slipid" ev ::1:::1 u ($1):($4-$2) w p ls ls_slipid lw pw pt pt_POPC_Na t "", \
    "boundDensity_vs_dOP_untilP.orange"        u ($1):($4-$2) ev :::::1 w l ls ls_orange t "Orange", \
    "boundDensity_vs_dOP_untilP.berger"        u ($1):($4-$2) ev :::::1 w l ls ls_berger t "Berger", \
    "boundDensity_vs_dOP_untilP.berger-OPLS"   u ($1):($4-$2) ev :::::1 w l ls ls_beOPLS t "Berger-OPLS", \
    "boundDensity_vs_dOP_untilP.macrog"        u ($1):($4-$2) ev :::::1 w l ls ls_macrog t "MacRog", \
    "boundDensity_vs_dOP_untilP.c36"           u ($1):($4-$2) ev :::::1 w l ls ls_chrm36 t "CHARMM36", \
    "boundDensity_vs_dOP_untilP.lipid14"       u ($1):($4-$2) ev :::::1 w l ls ls_lipd14 t "Lipid14", \
    "boundDensity_vs_dOP_untilP.ulmschneiders" u ($1):($4-$2) ev :::::1 w l ls ls_ulmsch t "Ulmschneiders", \
    "boundDensity_vs_dOP_untilP.slipid"        u ($1):($4-$2) ev :::::1 w l ls ls_slipid t "Slipid"
#    "boundDensity_vs_dOP_untilP.lipid14"       u ($1):(-4*2*0.49*20.5*0.68*$1/(3*167)) ev :::::1 w l ls ls_forKey t "Experimental", \
#  "boundDensity_vs_dOP_untilP.lipid14"       u ($1):(-4*2*0.49*20.5*0.58*$1/(3*167)):(-4*2*0.49*20.5*0.78*$1/(3*167)) ev :::::1 w filledcurves ls ls_forKey t "", \

set key at graph 0.37,1.08 width -6

set object 200 rectangle center graph 0.79, graph 0.98 size graph 0.34, graph 0.1 fs solid noborder
set object 201 rectangle center graph 0.79, graph 0.96 size graph 0.34, graph 0.25 fs empty
set label 1 at graph 0.635, graph 0.96 "POPC"
set label 2 at graph 0.635, graph 0.88 "DPPC"
set label 3 at graph 0.735, graph 1.04 "NaCl"
set label 4 at graph 0.855, graph 1.04 "CaCl_2"

set tmargin at screen top-heightY
set bmargin at screen top-2*heightY
set xlabel "bound cation charge (q/nm^{2}, integrated until P)" offset 0,0.5
set xtics format '%2.1f'
unset ylabel
set label 100 "{/Symbol a}"
plot [:][-0.17:0.03]\
  "boundDensity_vs_dOP_untilP.orange"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_orange t "", \
  "boundDensity_vs_dOP_untilP.berger"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_berger t "", \
  "boundDensity_vs_dOP_untilP.berger-OPLS"   u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_beOPLS t "", \
  "boundDensity_vs_dOP_untilP.macrog"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_macrog t "", \
  "boundDensity_vs_dOP_untilP.c36"           u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_chrm36 t "", \
  "boundDensity_vs_dOP_untilP.lipid14"       u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_lipd14 t "", \
  "boundDensity_vs_dOP_untilP.ulmschneiders" u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_ulmsch t "", \
  "boundDensity_vs_dOP_untilP.slipid"        u ($1):($4-$2+$3+$5):($4-$2-$3-$5) ev :::2 w filledcurves ls ls_slipid t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.orange"  ev :::2::2 u ($1):($4-$2) w p ls ls_orange lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.orange"  ev :::3::3 u ($1):($4-$2) w p ls ls_orange lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger"  ev :::3::3 u ($1):($4-$2) w p ls ls_berger lw pw pt pt_DPPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger"  ev :::4::4 u ($1):($4-$2) w p ls ls_berger lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.berger"  ev :::5::5 u ($1):($4-$2) w p ls ls_berger lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP.berger-OPLS"            ev ::1:2 u ($1):($4-$2) w p ls ls_beOPLS lw pw pt pt_DPPC_Na t "", \
   "boundDensity_vs_dOP_untilP.macrog"                 ev ::1:2 u ($1):($4-$2) w p ls ls_macrog lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.c36"     ev :::2::2 u ($1):($4-$2) w p ls ls_chrm36 lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.c36"     ev :::3::3 u ($1):($4-$2) w p ls ls_chrm36 lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::2::2 u ($1):($4-$2) w p ls ls_lipd14 lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP_typeseparation.lipid14" ev :::3::3 u ($1):($4-$2) w p ls ls_lipd14 lw pw pt pt_POPC_Ca t "", \
   "boundDensity_vs_dOP_untilP.ulmschneiders"          ev ::1:2 u ($1):($4-$2) w p ls ls_ulmsch lw pw pt pt_POPC_Na t "", \
   "boundDensity_vs_dOP_untilP.slipid"                 ev ::1:2 u ($1):($4-$2) w p ls ls_slipid lw pw pt pt_POPC_Na t "", \
    "boundDensity_vs_dOP_untilP.orange"        u ($1):($4-$2) ev :::2 w l ls ls_orange t "", \
    "boundDensity_vs_dOP_untilP.berger"        u ($1):($4-$2) ev :::2 w l ls ls_berger t "", \
    "boundDensity_vs_dOP_untilP.berger-OPLS"   u ($1):($4-$2) ev :::2 w l ls ls_beOPLS t "", \
    "boundDensity_vs_dOP_untilP.macrog"        u ($1):($4-$2) ev :::2 w l ls ls_macrog t "", \
    "boundDensity_vs_dOP_untilP.c36"           u ($1):($4-$2) ev :::2 w l ls ls_chrm36 t "", \
    "boundDensity_vs_dOP_untilP.lipid14"       u ($1):($4-$2) ev :::2 w l ls ls_lipd14 t "", \
    "boundDensity_vs_dOP_untilP.ulmschneiders" u ($1):($4-$2) ev :::2 w l ls ls_ulmsch t "", \
    "boundDensity_vs_dOP_untilP.slipid"        u ($1):($4-$2) ev :::2 w l ls ls_slipid t "", \
   "forKey.dat" ev :::0::0 w p ls ls_forKey pt pt_POPC_Na t "", \
   "forKey.dat" ev :::2::2 w p ls ls_forKey pt pt_POPC_Ca t "", \
   "forKey.dat" ev :::1::1 w p ls ls_forKey pt pt_DPPC_Na t ""
#    "boundDensity_vs_dOP_untilP.lipid14"       u ($1):(-4*2*20.5*0.68*$1/(3*167)) ev :::::1 w l ls ls_forKey t "", \
#  "boundDensity_vs_dOP_untilP.lipid14"       u ($1):(-4*2*20.5*0.58*$1/(3*167)):(-4*2*20.5*0.78*$1/(3*167)) ev :::2 w filledcurves ls ls_forKey t "", \


unset multiplot